set shell := ['/bin/bash', '-cu']
vm_name := "icos-devops-vm"
vault_password_file := "$HOME/.vault_password"
passwords_file := "$HOME/stuff/icos/dev_passwords.yml"

# TODO: Make variables configurable
repository_source := "local"

local_repository_path := "$HOME/stuff/icos/infrastructure"

# For remote fetching
private_key_file := "$HOME/.ssh/icos"
branch := "valter/dev"


# List available commands as default action
@_default:
    just --list --unsorted --justfile {{justfile()}}


fresh-test: reset-vm test

test:
  lxc file push ./scripts/test_dev_inventory.sh {{vm_name}}/root/test_dev_inventory.sh
  lxc exec {{vm_name}} -- bash test_dev_inventory.sh


setup-vm: launch-vm wait init-vm
reset-vm: destroy-vm setup-vm
enter-vm:
  lxc exec {{vm_name}} -- bash -c "source py_env/bin/activate && bash"
  
launch-vm:
  lxc launch ubuntu:24.04 {{vm_name}} --vm

stop-vm:
  lxc stop {{vm_name}}

start-vm:
  lxc start {{vm_name}}

destroy-vm:
  lxc delete --force {{vm_name}}

init-vm:
    #!/usr/bin/env bash
    case "{{repository_source}}" in
        (local)
            lxc config device add icos-devops-vm shared-infra-repo disk source={{local_repository_path}} path=/root/infrastructure
            ;;
        (remote)
            lxc file push {{private_key_file}} {{vm_name}}/root/.ssh/id_rsa
            lxc exec {{vm_name}} -- git clone git@github.com:ICOS-Carbon-Portal/infrastructure.git
            lxc exec {{vm_name}} -- bash -c "cd infrastructure && git checkout {{branch}}"
            ;;
        *) echo "Error: repository_source must be local or remote" && exit 1
    esac

    lxc file push {{passwords_file}} {{vm_name}}/root/infrastructure/devops/dev.inventory/group_vars/core_host/passwords.yml
    lxc file push {{vault_password_file}} {{vm_name}}/root/
    lxc file push ./scripts/setup_vm.sh {{vm_name}}/root/setup_vm.sh
    lxc exec {{vm_name}} -- bash setup_vm.sh




# TODO: Wait for VM to be available, and have internet connection, instead of sleep...
wait:
  sleep 20
