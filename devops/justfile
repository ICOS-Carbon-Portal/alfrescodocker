#!/usr/bin/env -S just --unstable --justfile

set positional-arguments
set shell := ['/bin/bash', '-cu']

editor := env_var_or_default("EDITOR", "emacsclient --no-wait")
inventory := "production"

export ANSIBLE_INVENTORY := inventory + ".inventory"

@_default:
    just --unstable --list --unsorted --justfile {{justfile()}}


# run a playbook
play playbook *args:
    ansible-playbook {{playbook}}.yml {{args}}

# run the install playbook
install *args:
    ansible-playbook install.yml {{args}}

# install and upgrade_everything
upgrade *args:
    ansible-playbook install.yml -eupgrade_everything=1 {{args}}

# show facts about a host
facts host:
    ansible {{host}} -m setup

# edit the vault
vault:
    EDITOR=emacsclient ansible-vault edit group_vars/all/vault.yml
