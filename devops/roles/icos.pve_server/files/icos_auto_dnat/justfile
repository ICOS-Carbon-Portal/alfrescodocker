set positional-arguments
set shell := ['/bin/bash', '-cu']


@_default:
    just --list --unsorted


# PROJECT
# run program using hatch
[group('project')]
run *args:
    hatch run "$@"

# show versions of installed dependencies
[group('project')]
freeze:
    hatch run pip freeze

# hatch env remove
[group('project')]
remove:
    hatch env remove

# remove all artifacts
[group('project')]
clean:
    fd -u __pycache__   -X rm -r
    fd -u .pytest_cache -X rm -r
    fd -u .mypy_cache   -X rm -r
    fd -u .coverage     -X rm -r
    rm -rf -- ./htmlcov ./build ./dist src/*.egg-info .ruff_cache



# RUN TEST COVERAGE
# -s      -- show output
# -k expr -- run only tests matching expr
# pytest
[group('pytest')]
test *args:
    hatch run pytest {{args}}

# list collected tests
[group('pytest')]
collect *args:
    hatch run pytest --collect-only {{args}}

# show results from coverage+pytest
[group('pytest')]
coverage:
    hatch run coverage run -m pytest
    hatch run coverage combine
    hatch run coverage html
    hatch run python3 -m webbrowser htmlcov/index.html



# RUFF
# ruff check
[group('ruff'),no-cd]
check *args=("."):
    hatch run ruff check "$@"

# ruff check --fix-only
[group('ruff'),no-cd]
fix *args=("."):
    hatch run ruff check --fix-only "$@"

# format python code with ruff
[group('ruff'),no-cd]
format *args=("."):
    hatch run ruff format "$@"



# MUTAGEN
# configure mutagen
[group('mutagen')]
mutconf host path="/opt/icos-auto-dnat":
    cog -r -Dhost={{host}} -Dpath={{path}} mutagen.yml

# start syncing using mutagen
[group('mutagen')]
mutup:
    -mutagen project terminate
    mutagen project start

# mutagen stop
[group('mutagen')]
mutdown:
    -mutagen project terminate
