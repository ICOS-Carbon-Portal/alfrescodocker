mut_host := "fsicos2.lunarc.lu.se"
mut_home := "/home/stiltweb"


@_default:
    just --list --unsorted --justfile {{justfile()}}


# TEST COVERAGE
# pytest
test *args:
    hatch run pytest {{args}}

# show results from coverage+pytest
coverage:
    hatch run coverage run -m pytest
    hatch run coverage html
    hatch run python3 -m webbrowser htmlcov/index.html

# remove all artifacts
clean:
    fd -u __pycache__   -X rm -r
    fd -u .pytest_cache -X rm -r
    fd -u .mypy_cache   -X rm -r
    fd -u .coverage     -X rm -r
    rm -rf -- ./htmlcov ./build ./dist src/*.egg-info .ruff_cache


# RUFF
# lint using ruff
lint *args=("."):
    hatch run ruff {{args}}

# lint --fix using ruff
fix *args=("."):
    hatch run ruff --fix {{args}}

# format python code with ruff
[no-cd]
format *args=("."):
    hatch run ruff format {{args}}


# MUTAGEN
# configure mutagen
mutconf:
    cog -r -Dhost={{mut_host}} -Dhome={{mut_home}} mutagen.yml

# start syncing using mutagen
mutup:
    -mutagen project terminate
    mutagen project start

# stop mutagen
mutdown:
    -mutagen project terminate
